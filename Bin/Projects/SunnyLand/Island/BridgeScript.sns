#import "MainScript"

class FallingBridge
{
    bool falling_bridge = false;
    uint falling_bridge_index = 0;
    float falling_bridge_time = 0.16f;

    array<PhysBox> bridge;
    array<PhysBox> bridge_dyn;

	bool init = false;

    SoundInstance@ sfx_bridge;

    void OnRespawnHero()
    {
        if (!init)
        {
            return;
        }

    	falling_bridge = false;
        falling_bridge_index = 0;

        for (int i = 0; i<bridge.get_length();i++)
        {
            bridge_dyn[i].visible = 0;
            bridge_dyn[i].phys.MoveTo(bridge_dyn[i].init_x, bridge_dyn[i].init_y);
        }

        for (int i = 0; i<bridge.get_length();i++)
        {
            bridge[i].visible = 1;
        }
    }

    void OnActivateTrigger(int self_index, string&in name, int index)
    {
        if (name == "Hero")
        {
            if (falling_bridge_index == 0)
            {
                sfx_bridge.Play(1);

                falling_bridge = true;
            }
        }
    }

	void Update(float dt)
	{
		if (!init)
        {
            @sfx_bridge = core.sound.CreateSound("SFX/FallingBridge.ogg");

			for (int i = 0; i<bridge_dyn.get_length();i++)
            {
                bridge_dyn[i].init_x = bridge_dyn[i].x;
                bridge_dyn[i].init_y = bridge_dyn[i].y;

                bridge_dyn[i].phys.MoveTo(bridge_dyn[i].init_x, bridge_dyn[i].init_y);
            }

            init = true;
        }

		if (falling_bridge)
        {
            falling_bridge_time -= dt;

            if (falling_bridge_time < 0.0f)
            {
                falling_bridge_time += 0.095f;

                bridge[falling_bridge_index].visible = 0;
                bridge_dyn[falling_bridge_index].visible = 1;
                falling_bridge_index++;

                if (falling_bridge_index == bridge.get_length())
                {
                    sfx_bridge.Stop();
                    falling_bridge = false;
                }
            }
        }
	}
}