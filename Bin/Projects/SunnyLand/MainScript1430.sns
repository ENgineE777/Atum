
#import "GameMenuScript"

external shared class GameMenu;

shared class BaseCharacter
{
    float x, y;
    int horz_flipped;
    int visible;
}

shared class Bonus
{
    float x, y;
    int visible;
    int score;
}

shared class PhysBox
{
    float x, y;
    int visible;
    float init_x, init_y;
    Phys2D@ phys;
}

shared class ThrowableBox
{
    float x, y;
    int visible;
    int contact_nums = 0;
    float in_fly = -1.0f;
    Phys2D@ phys;
}

shared class ChracterData : BaseCharacter
{
    Phys2D@ phys;
    Graph2D@ graph;

    float velocity_x = 0.0f;
    float velocity_y = 0.0f;
    float move_speed = 400.0f;
    float accel_speed = 700.0f;
    float stop_speed = 1500.0f; 
    float jump_impulse = -700.0f;

    float accel_gravity = 400.0f;
    float max_gravity = 900.0f;

    float dir_x = 0.0f;

    int hp = 3;
    bool horz_flipped_inv = false;

    void Init()
    {
    }
}

shared class HeroData : ChracterData
{
    int score = 0;
    int box_grabbed = -1;
    //int card_grabbed = -1;
    float in_hit = -1.0f;
    int cur_respawn_point = 0;
}

shared class Game
{
    array<Bonus> diamonds;
    array<Bonus> cherries;
    array<BaseCharacter> eagles;
    array<BaseCharacter> opposums;
    array<PhysBox> respawn_points;
    array<PhysBox> boxes;
    array<PhysBox> fake_box;
    array<ThrowableBox> fly_boxes;
    array<HeroData> hero_inst;
    Camera2D@ camera;
    Sprite@ item_taken;
    Sprite@ enemy_death;
    VirtualJoystick@ vjoy;

    GameMenu@ game_menu;

    float fade_out = -1.0f;

    bool is_paused = false;

    bool init = false;

    float return_to_title = -1.0f;

    float title_menu_pos_x = 0.0f;
    float label_fade = 2.0f;

    void OnStartGame()
    {
        is_paused = false;

        for (int i = 0; i<diamonds.length();i++)
        {
            diamonds[i].visible = 1;
        }

        for (int i = 0; i<cherries.length();i++)
        {
            cherries[i].visible = 1;
        }

        game_menu.ShowPauseMenu(0);

        fade_out = -1.0f;
        hero_inst[0].cur_respawn_point = 0;
        AddScore(hero_inst[0], -1);
        RespawnHero(hero_inst[0]);
    }

    int FindBox(float pos_x, float pos_y)
    {
        for (uint i=0; i<boxes.length(); i++)
        {
            if (boxes[i].visible == 0)
            {
                continue;
            }

            if (abs(boxes[i].x + 32 - pos_x) < 15 && abs(boxes[i].y - 32 - pos_y) < 32)
            {
                return i;
            }
        }

        return -1;
    }

    /*int FindCard(float pos_x, float pos_y)
    {
        for (uint i=0; i<boss_cards.length(); i++)
        {
            if (boss_cards[i].visible == 0)
            {
                continue;
            }

            if (abs(boss_cards[i].x - pos_x) < 15 && abs(boss_cards[i].y - 32 - pos_y) < 32)
            {
                return i;
            }
        }

        return -1;
    }*/

    void HitHero(HeroData@ hero_data, float x)
    {
        if (hero_data.in_hit < -0.01f)
        {
            hero_data.hp--;
            game_menu.UpdateHearts(hero_data.hp);

            if (hero_data.hp == 0)
            {
                fade_out = 2.0f;
            }

            if (hero_data.box_grabbed != -1)
            {
                fake_box[0].visible = 0;
                enemy_death.AddInstance(fake_box[0].x, fake_box[0].y, true);
            }

            /*if (hero_data.card_grabbed != -1)
            {
                fake_card[0].visible = 1;
                enemy_death.AddInstance(fake_card[0].x, fake_card[0].y, true);
            }*/

            hero_data.graph.GotoNode("Hit");
            hero_data.in_hit = 1.0f;

            hero_data.box_grabbed = -1;
            //hero_data.card_grabbed = -1;

            //hero_velocity_x = 
            //hero_velocity_y = 200.0f;
            //jump_impulse

            //hero.ApplyLinearImpulse(20.0f * (x < hero.x ? 1.0f : -1.0f), -7);
        }
    }

    void RespawnHero(HeroData@ hero_data)
    {
        hero_data.phys.MoveTo(respawn_points[hero_data.cur_respawn_point].x, respawn_points[hero_data.cur_respawn_point].y);
        hero_data.hp = 3;
        hero_data.in_hit = -1.0f;
        //fake_box[0].visible = 0;
        //fake_card[0].visible = 0;
        game_menu.UpdateHearts(hero_data.hp);

        hero_data.graph.GotoNode("Idle");

        hero_data.velocity_x = 0.0f;
        hero_data.velocity_y = 0.0f;

        return_to_title = -1.0f;

        hero_data.box_grabbed = -1;
        //hero_data.card_grabbed = -1;

        item_taken.ClearInstances();
        enemy_death.ClearInstances();

        for (int i = 0; i<eagles.length();i++)
        {
            eagles[i].visible = 1;
        }

        for (int i = 0; i<opposums.length();i++)
        {
            opposums[i].visible = 1;
        }

        for (int i = 0; i<fly_boxes.length();i++)
        {
            fly_boxes[i].visible = 0;
            fly_boxes[i].in_fly = -1.0f;
        }

        /*for (int i = 0; i<boss_cards.length();i++)
        {
            boss_cards[i].visible = 0;
            boss_cards[i].in_fly = -1.0f;
        }*/

        for (int i = 0; i<boxes.length();i++)
        {
            boxes[i].visible = 1;
            boxes[i].phys.MoveTo(boxes[i].init_x, boxes[i].init_y);
        }

        Script_CallClassInstancesMethod("", "", "OnRespawnHero");
    }

    void AddScore(HeroData@ hero_data, int score)
    {
        if (score == -1)
        {
            hero_data.score = 0;
        }
        else
        {
            hero_data.score += score;
        }

        game_menu.SetScore(hero_data.score);
    }

    void OnHeroContact(int self_index, string&in name, int index)
    {
        if (name == "CherryItem")
        {
            AddScore(hero_inst[0], cherries[index].score);
            cherries[index].visible = 0;
            item_taken.AddInstance(cherries[index].x, cherries[index].y, true);
        }
        else
        if (name == "DiamondItem")
        {
            AddScore(hero_inst[0], diamonds[index].score);
            diamonds[index].visible = 0;
            item_taken.AddInstance(diamonds[index].x, diamonds[index].y, true);
        }
        else
        if (name == "EagleFly")
        {
            HitHero(hero_inst[0], eagles[index].x);
        }
        else
        if (name == "OpposumRun")
        {
            HitHero(hero_inst[0], opposums[index].x);
        }
    }

    void OnBoxContact(int self_index, string&in name, int index)
    {
        if (name == "Hero")
        {
            return;
        }

        if (name == "EagleFly")
        {
            eagles[index].visible = 0;
            enemy_death.AddInstance(eagles[index].x, eagles[index].y, true);
        }

        if (name == "OpposumRun")
        {
            opposums[index].visible = 0;
            enemy_death.AddInstance(opposums[index].x, opposums[index].y, true);
        }

        fly_boxes[0].visible = 0;
        fly_boxes[0].in_fly = -1.0f;
        enemy_death.AddInstance(fly_boxes[0].x, fly_boxes[0].y, true);
    }

    void OnActivateResapwnTrigger(int self_index, string&in name, int index, int param)
    {
        if (name == "Hero")
        {
            hero_inst[0].cur_respawn_point = param;
        }
    }

    void OnRespawnTrigger(int self_index, string&in name, int index)
    {
        if (name == "Hero" && fade_out < 0.01f)
        {
            fade_out = 2.0f;
        }
    }

    void MoveCharacter(ChracterData@ data, float dt)
    {
        if (data.dir_x < -0.01f)
        {
            data.horz_flipped = data.horz_flipped_inv ? 0 : 1;

            data.velocity_x -= dt * data.accel_speed;

            if (data.velocity_x < -data.move_speed)
            {
                data.velocity_x = -data.move_speed;
            }
        }
        else
        if (data.dir_x > 0.01f)
        {
            data.horz_flipped = data.horz_flipped_inv ? 1 : 0;

            data.velocity_x += dt * data.accel_speed;

            if (data.velocity_x > data.move_speed)
            {
                data.velocity_x = data.move_speed;
            }
        }
        else
        {
            if (data.velocity_x > 0.0001f)
            {
                data.velocity_x -= dt * data.stop_speed;

                if (data.velocity_x < 0.0f)
                {
                    data.velocity_x = 0.0f;
                }
            }
            else
            if (data.velocity_x < -0.0001f)
            {
                data.velocity_x += dt * data.stop_speed;

                if (data.velocity_x > 0.0f)
                {
                    data.velocity_x = 0.0f;
                }
            }
        }

        data.velocity_y += dt * data.accel_gravity;

        if (data.velocity_y > data.max_gravity)
        {
            data.velocity_y = data.max_gravity;
        }

        data.phys.Move(data.velocity_x * dt, data.velocity_y * dt);
    }

    void ControlPlayer(HeroData@ hero_data, float dt)
    {
        bool allow_jump = hero_data.phys.CheckColission(true);

        if (!allow_jump && hero_data.velocity_y > 20.5f)
        {
            hero_data.graph.ActivateLink("fall");
        }
        
        if (Control_GetDebugState("KEY_A", 0) > 0 || vjoy.stick_delta_x < -0.5f)
        {
            if (allow_jump)
            {
                hero_data.graph.ActivateLink("run");
            }

            hero_data.dir_x = -1.0f;
        }
        else
        if (Control_GetDebugState("KEY_D", 0) > 0 || vjoy.stick_delta_x > 0.5f)
        {
            if (allow_jump)
            {
                hero_data.graph.ActivateLink("run");
            }

            hero_data.dir_x = 1.0f;
        }
        else
        {
            hero_data.dir_x = 0.0f;

            if (allow_jump)
            {
                hero_data.graph.ActivateLink("idle");
            }
        }

        if (allow_jump && hero_data.velocity_y > 25.0f)
        {
            hero_data.velocity_y = 0.0f;
        }
        else
        {
            if (hero_data.velocity_y < 0.0f)
            {
                if (hero_data.phys.CheckColission(false))
                {
                    hero_data.velocity_y = 0.0f;
                }
            }

            hero_data.velocity_y += dt * hero_data.accel_gravity;

            if (hero_data.velocity_y > hero_data.max_gravity)
            {
                hero_data.velocity_y = hero_data.max_gravity;
            }
        }

        MoveCharacter(hero_data, dt);

        if ((Control_GetDebugState("KEY_W", 1) > 0 || vjoy.button_a_pressed == 2) && allow_jump)
        {
            hero_data.graph.ActivateLink("jump");
            hero_data.velocity_y = hero_data.jump_impulse;
        }

        if (Control_GetDebugState("KEY_L", 1) > 0 || vjoy.button_b_pressed == 2)
        {
            if (hero_data.box_grabbed == -1 /*&& hero_data.card_grabbed == -1*/)
            {
                if (fly_boxes[0].in_fly > 0.0f)
                {
                    fly_boxes[0].visible = 0;
                    fly_boxes[0].in_fly = -1.0f;
                    enemy_death.AddInstance(fly_boxes[0].x, fly_boxes[0].y, true);
                }

                hero_data.box_grabbed = FindBox(hero_data.x - 64 * (hero_data.horz_flipped > 0 ? 1 : -1), hero_data.y - 32);

                if (hero_data.box_grabbed != -1)
                {
                    fake_box[0].visible = 1;
                    boxes[hero_data.box_grabbed].visible = 0;
                }

                /*hero_data.card_grabbed = FindCard(hero_data.x - 64 * (hero_data.horz_flipped > 0 ? 1 : -1), hero_data.y - 32);

                if (hero_data.card_grabbed != -1)
                {
                    fake_card[0].visible = 1;
                    boss_cards[hero_data.card_grabbed].visible = 0;
                }*/
            }
            else
            if (hero_data.box_grabbed != -1)
            {
                fly_boxes[0].visible = 1;
                fly_boxes[0].x = fake_box[0].x;
                fly_boxes[0].y = fake_box[0].y;
                fly_boxes[0].in_fly = 2.0f;

                fake_box[0].visible = 0;

                fly_boxes[0].phys.ApplyLinearImpulse((hero_data.horz_flipped > 0 ? -1 : 1) * 110, 0);
                hero_data.box_grabbed = -1;
            }
            /*else
            if (hero_data.card_grabbed != -1)
            {
                boss_cards[3].visible = 1;
                boss_cards[3].x = fake_card[0].x;
                boss_cards[3].y = fake_card[0].y;
                boss_cards[3].in_fly = 2.0f;

                fake_card[0].visible = 0;

                boss_cards[3].phys.ApplyLinearImpulse((hero_data.horz_flipped > 0 ? -1 : 1) * 110, 0);
                hero_data.card_grabbed = -1;
            }*/
        }

        if (hero_data.box_grabbed != -1)
        {
            fake_box[0].x = hero_inst[0].x;
            fake_box[0].y = hero_inst[0].y - 114;
        }

        /*if (hero_data.card_grabbed != -1)
        {
            fake_card[0].x = hero_inst[0].x;
            fake_card[0].y = hero_inst[0].y - 114;
        }*/
    }

    void CheckFlyBoxes(array<ThrowableBox>@ boxes, float dt)
    {
        for (uint i = 0; i < boxes.length(); i++)
        {
            if (boxes[i].visible == 0)
            {
                continue;
            }

            if (boxes[i].in_fly > 0.0f)
            {
                boxes[i].in_fly -= dt;

                if (boxes[i].in_fly < 0.0f)
                {
                    boxes[i].visible = 0;
                    enemy_death.AddInstance(boxes[i].x, boxes[i].y, true);
                    boxes[i].in_fly = -1.0f;
                }
            }
        }
    }

    void Update(float dt)
    {
        if (!init)
        {
            for (int i = 0; i<boxes.length();i++)
            {
                boxes[i].init_x = boxes[i].x;
                boxes[i].init_y = boxes[i].y;
            }

            hero_inst[0].Init();
            OnStartGame();

            init = true;

            return;
        }

        if (is_paused)
        {
            /*if (pause_menu.GetState() != 2)
            {
                title_menu_pos_x += dt * 1000;
                label_fade -= dt;

                //start_label.alpha = (label_fade > 1.0f) ? 1.0f : 0.0f;

                while (label_fade < 0.0f)
                {
                    label_fade += 2.0f;
                }

                camera.target_pos_x = title_menu_pos_x;
                camera.x = camera.target_pos_x;
                camera.target_pos_y = -800;
                camera.y = camera.target_pos_y;
            }*/

            return;
        }

        if (hero_inst[0].in_hit < -0.01f && hero_inst[0].hp > 0)
        {
            ControlPlayer(hero_inst[0], dt);
        }
        else
        {
            hero_inst[0].in_hit -= dt;

            if (hero_inst[0].in_hit < 0.0f)
            {
                hero_inst[0].in_hit = -1.0f;
                hero_inst[0].graph.ActivateLink("idle");
            }
        }

        CheckFlyBoxes(fly_boxes, dt);

        if (fade_out > 0.0f)
        {
            float prev_fade_out = fade_out;
            fade_out -= dt * 2.0f;
            float fade_alpha = 0.0f; 

            if (fade_out > 1.0f)
            {
                fade_alpha = 2.0f - fade_out;
            }
            else
            {
                if (prev_fade_out > 1.0f)
                {
                    RespawnHero(hero_inst[0]);
                }

                fade_alpha = fade_out;
            }

            if (fade_out < 0.01f)
            {
                fade_out = -1.0f;
                fade_alpha = 0.0f;
            }

            game_menu.SetFade(fade_alpha);
        }

        if (return_to_title > 0.0f)
        {
            return_to_title -= dt;

            if (return_to_title < 0.01f)
            {
                return_to_title = -1.0f;
                game_menu.OnReturnToTitle();
            }
        }

        if (fade_out < 1.0f)
        {
            camera.target_pos_x = hero_inst[0].x;
            camera.target_pos_y = hero_inst[0].y;
        }
    }
}